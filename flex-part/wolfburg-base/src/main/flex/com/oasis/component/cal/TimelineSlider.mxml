<?xml version="1.0" encoding="utf-8"?>
<!--
Copyright 2007 Misko Hevery <misko@hevery.com>

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
-->
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:utils="flash.utils.*"
	creationComplete="init()">
	<mx:Script>
		<![CDATA[
			import mx.events.SliderEvent;
			import mx.controls.sliderClasses.Slider;
		
			[Bindable]
			public var selectedTime:Date = new Date();
			[Bindable]
			public var scrollSize:Number = DateUtil.DAY;
			[Bindable]
			public var multiplier:Number = 1;
			
			private var timer:Timer = new Timer(50);
			private var lastTick:Number;
			private var isThumDragging:Boolean = false;
			
			private function init():void {
				timer.addEventListener(TimerEvent.TIMER, onTimerTick);
			}
			
			private function onTimerTick(event:TimerEvent):void {
				var now:Number = new Date().time;
				var elapsedTime:Number = (now - lastTick) / 1000.0;
				selectedTime = new Date(selectedTime.time + slider.value * elapsedTime);
				lastTick = now;
			}
			
			private function onThumbPress(slider:Slider):void {
				isThumDragging = true;
				lastTick = new Date().time;
				timer.start();
			}
			
			private function onThumbRelease(slider:Slider):void {
				timer.stop();
				slider.value = 0;
				isThumDragging = false;
			}
			
			private function onChange(slider:Slider):void {
				if (!isThumDragging) {
					selectedTime = new Date(selectedTime.time + slider.value);
					slider.value = 0;
				}
			}
		]]>
	</mx:Script>
	<mx:HSlider id="slider" width="100%"
		thumbPress="onThumbPress(event.currentTarget as Slider)"
		thumbRelease="onThumbRelease(event.currentTarget as Slider)"
		change="onChange(event.currentTarget as Slider)"
		minimum="{-scrollSize * multiplier}" maximum="{scrollSize * multiplier}" value="0"
		showDataTip="false"
		labels="['Past', 'Now' , 'Future']" liveDragging="true"
		/>
</mx:HBox>
