<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:mx="library://ns.adobe.com/flex/mx"  
		 xmlns:fb="http://flexbase.800best.com/mxml"
		 width="100%" height="100%"
		 xmlns:grid="com.oasis.component.grid.*"
		 xmlns:v5="com.oasis.tmsv5.view.report.*"	
		 addedToStage="init();" xmlns:code="http://code.google.com/p/flexlib/" xmlns:PremissionButton="com.oasis.component.PremissionButton.*">
	<fx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			import com.best.oasis.flexbase.util.ValidationUtils;
			import com.oasis.component.page.PageList;
			import com.oasis.tmsv5.truckRSSchedule.TruckRSJobController;
			import com.oasis.tmsv5.truckRSSchedule.TruckRSJobTimeRecordController;
			import com.oasis.tmsv5.utils.DateSearchUtils;
			import com.oasis.tmsv5.view.popup.AdjustRecordDialog;
			import com.oasis.tmsv5.view.popup.ExpTrackForJobDialog;
			import com.oasis.tmsv5.view.util.ShowExcelErrorDialog;
			import com.oasis.utils.DateFormatterUtil;
			import com.oasis.wolfburg.enum.EnumManage;
			import com.oasis.wolfburg.so.truckRSSchedule.TruckRSJobTimeRecordSO;
			import com.oasis.wolfburg.vo.excel.ExcelParsePackageVO;
			import com.oasis.wolfburg.vo.truckRSSchedule.TRSJobTimeRecordMaintainView;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.core.FlexGlobals;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.utils.URLUtil;
			
			[Bindable]
			private var pageList:PageList = new PageList();
			
			[Bindable]
			private var so:TruckRSJobTimeRecordSO = new TruckRSJobTimeRecordSO();
			
			[Inject]
			public var truckRSJobTimeRecordController:TruckRSJobTimeRecordController;
			
			[Inject]
			public var truckRsJobController:TruckRSJobController;
			
			[Bindable]
			public var truckTypeArray:ArrayCollection = EnumManage.getTruckType4list();
			
			[Dispatcher]
			public var dispatch:IEventDispatcher;
			
			/**
			 * 文件下载地址前缀
			 */
			private var downloadUrlPrefix:String;
			
			/**
			 * 批量导入文件上传句柄
			 */
			private var batchImportFileReference:FileReference=new FileReference();
			
			public var host:String;
			
			private function init():void{
				so.beginDate = searchForm.billDateBegin.date;
				so.endDate = searchForm.billDateEnd.date;
				search();
			}
			
			private function getPageData(pageNum:int, pageSize:int):void{
				so.pageNumber = pageNum;
				so.objectsPerPage = pageSize;
				truckRSJobTimeRecordController.getMaintainPageList(so, pageList);
			}
			
			private function search():void{
				getPageData(1, so.objectsPerPage);
			}
			
			private function getStatusLabel(item:Object,column:DataGridColumn):String{
				var status:Boolean = item.disabled as Boolean;
				if(status){
					return '锁定';
				}else{
					return '正常';
				}
			}
			
			private function getTruckType(item:Object,column:DataGridColumn):String{
				var truckType:String = item.truckType as String
				return EnumManage.getDisplayText(truckType,truckTypeArray);
			}
			
			private function getStartDate(item:Object,column:DataGridColumn):String{
				var date:Date = (item as TRSJobTimeRecordMaintainView).startDate;
				return DateFormatterUtil.format6(date);
			}
			
			private function getPlainTime(item:Object,column:DataGridColumn):String{
				var date:Date = (item as TRSJobTimeRecordMaintainView).planTime;
				return DateFormatterUtil.format6(date);
			}
			
			private function getJobStatus(item:Object,column:DataGridColumn):String{
				return EnumManage.getDisplayText(item.status,EnumManage.getRSJobStatus());
			}
			
			public function popupExp(id:Number):void {
				var dialog:ExpTrackForJobDialog = 
					ExpTrackForJobDialog(PopUpManager.createPopUp(root, ExpTrackForJobDialog , true));
				dialog.rsJobId = id;
				PopUpManager.centerPopUp(dialog);
			}
			
			public function adjustRecord(data:Object,jobId:Number,order:int):void {
				if(data == null){
					return;
				}
				var dialog:AdjustRecordDialog = 
					AdjustRecordDialog(PopUpManager.createPopUp(root, AdjustRecordDialog , true));
				dialog.vo = data as TRSJobTimeRecordMaintainView;
				dialog.passFun = function():void{
					getPageData(so.pageNumber, so.objectsPerPage);
				};
				PopUpManager.centerPopUp(dialog);
			}
			/**
			 * 批量导入
			 */
			private function batchImport():void {
				batchImportFileReference.browse(getTypeFilter());
				batchImportFileReference.addEventListener(Event.SELECT, batchImportSelectHandler);
				batchImportFileReference.addEventListener(Event.COMPLETE, batchImportCompleteHandler);
			}
			
			private function getTypeFilter():Array {
				var fileFilter:FileFilter=new FileFilter("Excel文件 (*.xls)", "*.xls");
				return [fileFilter];
			}
			
			/**
			 * 批量导入文件选择后的处理函数
			 * 选择文件后，现在本地做读取动作*注意一定要 FLASH PLAYER 10 以上才有load方法
			 */
			private function batchImportSelectHandler(event:Event):void {
				batchImportFileReference.load();
			}
			/**
			 * 批量导入上传文件回调函数
			 */
			private function batchImportCompleteHandler(event:Event):void {
				var data:ByteArray=new ByteArray();
				batchImportFileReference.data.readBytes(data, 0, batchImportFileReference.data.length);
				truckRSJobTimeRecordController.batchImport(data, batchImportCompleteCallBack, failCallBack);
			}
			/**
			 * 失败回调
			 */ 
			private function failCallBack(event:FaultEvent):void{
				Alert.show(event.fault.faultString);
			}
			/**
			 * 导入数据后的回调函数
			 */
			private function batchImportCompleteCallBack(excelParsePackage:ExcelParsePackageVO):void {
				if (excelParsePackage == null){
					return;
				}
				if (excelParsePackage.errors == null || excelParsePackage.errors.length == 0) {
					Alert.show("成功批量导入" + excelParsePackage.totalNum + "条考勤打卡记录！");
				} else {
					var dialog:ShowExcelErrorDialog=ShowExcelErrorDialog(PopUpManager.createPopUp(root, ShowExcelErrorDialog, true));
					PopUpManager.centerPopUp(dialog);
					dialog.errors=excelParsePackage.errors;
					dialog.init();
				}
				getPageData(1,so.objectsPerPage);
			}
			/**
			 * 导入模板下载
			 */
			private function templateDownload():void{
				var fileRef:FileReference = new FileReference();  
				var path:String = "/wolfburg-war/download/template/OperationTimeManageImport_template.xls";
				var downloadUrl:String = getRequestURL() + path;
				var urlReq:URLRequest = new URLRequest(downloadUrl); 
				fileRef.addEventListener(IOErrorEvent.IO_ERROR, handleError);
				fileRef.addEventListener(Event.COMPLETE, saveComplete);
				fileRef.download(urlReq);
			}
			public function saveComplete(event:Event):void 
			{
				Alert.show("文件保存成功！");
			}
			public function handleError(event:IOErrorEvent):void 
			{
				Alert.show("模版文件下载失败！模版文件不存在或者有同名文件处于打开状态。");
			}
			private function getRequestURL():String{
				var url:String = FlexGlobals.topLevelApplication.url;
				var fullUrl:String = URLUtil.getFullURL(url, url);
				var serverNameWithPort:String = URLUtil.getServerNameWithPort(url);
				var sdex:int = fullUrl.indexOf(serverNameWithPort);
				sdex = fullUrl.indexOf("/",sdex);
				host = fullUrl.substr(0,sdex);
				return host;
			}
		]]>
	</fx:Script>
	<mx:VBox height="100%" width="100%">
		<code:WindowShade id="ws1" styleName="linkButtonWindowShade" width="100%" label="查询" height="23%">
			<v5:OperationTimeRecordSearchForm id="searchForm" so="{so}" searchFunction="{search}" height="125" width="100%"/>
		</code:WindowShade>	
		<mx:HBox width="100%">
			<PremissionButton:PremissionButton label="批量导入" click="batchImport();" module="OperationTimeManage" premission="batchImport"/>
			<PremissionButton:PremissionButton label="导入模板下载" click="templateDownload();" module="OperationTimeManage" premission="templateDownload"/>
		</mx:HBox>
		<fb:PageableDataGrid id="grid" dataProvider="{pageList.list}" width="100%"
			 getPageData="getPageData"  currentPageNum="{so.pageNumber}" totalItemNum="{pageList.fullListSize}" 
			 pageSize="{so.objectsPerPage}" useLocalStore="false" sortableColumns="false"
			 height="100%" showControlColumn="false" horizontalScrollPolicy="on">
			<fb:columns >
				
				<fb:DataGridColumnNested headerText="执行状态" dataField="jobStatus"  width="80" labelFunction="getJobStatus" sortable="false"/>
				<fb:DataGridColumnNested headerText="任务单名称" dataField="code"  width="100" sortable="false"/>
				<fb:DataGridColumnNested headerText="线路名称" dataField="routeName"  sortable="false"/>
				<fb:DataGridColumnNested headerText="车牌号" dataField="licensePlate"  width="100" sortable="false"/>
				<fb:DataGridColumnNested headerText="计划出发" labelFunction="getStartDate"  width="150" sortable="false"/>
				<fb:DataGridColumnNested headerText="站序" dataField="stopSeq"  width="50" sortable="false"/>
				<fb:DataGridColumnNested headerText="站点" dataField="thisStop"  width="100" sortable="false"/>
				<fb:DataGridColumnNested headerText="到发站" dataField="scanedType"  width="60" sortable="false"/>
				<fb:DataGridColumnNested headerText="计划时间" dataField="plainTime" width="150" labelFunction="getPlainTime" sortable="false"/>
				<fb:DataGridColumnNested headerText="扫描时间" width="150" sortable="false">
					<fb:itemRenderer>
						<fx:Component>
							<mx:LinkButton label="{data.scanedTime!=null?DateFormatterUtil.format2(data.scanedTime):'调整时间'}" color="0x3380DD" click="outerDocument.adjustRecord(data,data.jobId,7);"
										   textDecoration="underline" fontWeight="normal" >
								<fx:Script>
									<![CDATA[
										import com.oasis.utils.DateFormatterUtil;
									]]>
								</fx:Script>
							</mx:LinkButton>
						</fx:Component>
					</fb:itemRenderer>
				</fb:DataGridColumnNested>
				<fb:DataGridColumnNested headerText="车牌号" dataField="licensePlate" width="80" sortable="false"/> 
				<fb:DataGridColumnNested headerText="车卡号" dataField="idCard" width="100" sortable="false"/>
				<fb:DataGridColumnNested headerText="车型" dataField="truckType" width="60" labelFunction="getTruckType" sortable="false"/>
				<fb:DataGridColumnNested headerText="联系电话" dataField="contactPhone1" width="100" sortable="false"/>
				
				
			</fb:columns>
		</fb:PageableDataGrid>
	</mx:VBox>
</mx:VBox>
